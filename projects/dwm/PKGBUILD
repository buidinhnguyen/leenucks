pkgname=dwm
pkgver=6.0
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11')
source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz \
        config.def.h
        dwm-$pkgver-centered-floating.diff)
. md5sums
build() {
  cd "$srcdir/$pkgname-$pkgver"

  cp "$srcdir"/config.def.h config.h

  patch -p1 < ../dwm-"$pkgver"-centered-floating.diff

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
  sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package(){
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -m644 -D LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
  install -m644 -D README "$pkgdir"/usr/share/doc/"$pkgname"/README
}
